/**
* TargetGroupQueries
* 
* A list of queries that enrich the TargetGroup and TargetGroupCfg with operations to easier query their state.
*
*/
library TargetGroupQueries;

modeltype SPD_MOD uses 'http://palladiosimulator.org/ScalingPolicyDefinition/1.0';
modeltype SPD_ADJ uses 'http://palladiosimulator.org/ScalingPolicyDefinition/Adjustments/1.0';
modeltype SPD_TRI uses 'http://palladiosimulator.org/ScalingPolicyDefinition/Triggers/1.0';
modeltype SPD_TAR uses 'http://palladiosimulator.org/ScalingPolicyDefinition/Targets/1.0';

modeltype SPD_CONT uses 'http://palladiosimulator.org/ScalingPolicyDefinition/Constraints/Target/1.0';
modeltype SPD_SEM uses 'http://palladiosimulator.org/ScalingPolicyDefinitionSemantic/1.0';


modeltype PCM uses 'http://palladiosimulator.org/PalladioComponentModel/5.2';
modeltype PCM_ALLOC uses 'http://palladiosimulator.org/PalladioComponentModel/Allocation/5.2';
modeltype PCM_REP uses 'http://palladiosimulator.org/PalladioComponentModel/Repository/5.2';
modeltype PCM_SEFF uses 'http://palladiosimulator.org/PalladioComponentModel/SEFF/5.2';
modeltype PCM_SYS uses 'http://palladiosimulator.org/PalladioComponentModel/System/5.2';
modeltype PCM_RES_TYPE uses 'http://palladiosimulator.org/PalladioComponentModel/ResourceType/5.2';
modeltype PCM_CORE uses 'http://palladiosimulator.org/PalladioComponentModel/Core/5.2';
modeltype PCM_COMP uses 'http://palladiosimulator.org/PalladioComponentModel/Core/Composition/5.2';
modeltype PCM_ENTITY uses 'http://palladiosimulator.org/PalladioComponentModel/Core/Entity/5.2';
modeltype PCM_PARAM uses 'http://palladiosimulator.org/PalladioComponentModel/Parameter/5.2';
modeltype PCM_USG uses 'http://palladiosimulator.org/PalladioComponentModel/UsageModel/5.2';


query TargetGroupCfg::numberOfElements():Integer{
	switch { 
		case (self.oclIsKindOf(ServiceGroupCfg)) {
			return self.oclAsType(ServiceGroupCfg).elements->size();		
		} 
		case (self.oclIsKindOf(CompetingConsumersGroupCfg)) {
			return self.oclAsType(CompetingConsumersGroupCfg).elements->size();
		} 
		case (self.oclIsKindOf(ElasticInfrastructureCfg)){
			return self.oclAsType(ElasticInfrastructureCfg).elements->size();
		}
		else {
			return assert fatal(false) with log('Number of elements invoked for an unknown subtype of TargetGroupCfg');
		} 
	};
}

query OrderedSet(TargetGroupCfg)::retrieveServiceTargetCfgOfPolicy(policy:ScalingPolicy) : TargetGroupCfg {
	return self->selectOne(cfg | cfg.retrieveUnit() = policy.targetGroup.retrieveUnit());
}

query TargetGroup::retrieveUnit():AssemblyContext {
	switch { 
		case (self.oclIsKindOf(ServiceGroup)) {
			return self.oclAsType(ServiceGroup).unitAssembly.oclAsType(AssemblyContext);		
		} 
		case (self.oclIsKindOf(CompetingConsumersGroup)) {
			return self.oclAsType(CompetingConsumersGroup).unitAssembly.oclAsType(AssemblyContext);
		} 
	};
	return assert fatal (false) with log('cannot retrieve unit assembly for non-services');	
}

query TargetGroupCfg::retrieveAssemblyElements():OrderedSet(AssemblyContext){
	switch { 
		case (self.oclIsKindOf(ServiceGroupCfg)) {
			return self.oclAsType(ServiceGroupCfg).elements;		
		} 
		case (self.oclIsKindOf(CompetingConsumersGroupCfg)) {
			return self.oclAsType(CompetingConsumersGroupCfg).elements;
		} 
	};
	return assert fatal (not self.isServicePattern()) with log('cannot retrieve assembly elements for non-services');	
}

query TargetGroupCfg::retrieveUnit():AssemblyContext{
	switch { 
		case (self.oclIsKindOf(ServiceGroupCfg)) {
			return self.oclAsType(ServiceGroupCfg).unit;		
		} 
		case (self.oclIsKindOf(CompetingConsumersGroupCfg)) {
			return self.oclAsType(CompetingConsumersGroupCfg).unit;
		} 
	};
	return assert fatal (not self.isServicePattern()) with log('cannot retrieve unit assembly for non-services');	
}

query TargetGroupCfg::isServicePattern():Boolean {
	return self.oclIsKindOf(ServiceGroupCfg) or self.oclIsKindOf(CompetingConsumersGroupCfg);
}

query TargetGroup::isServicePattern():Boolean {
	return self.oclIsKindOf(ServiceGroup) or self.oclIsKindOf(CompetingConsumersGroup);
}